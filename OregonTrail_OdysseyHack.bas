10 REM PROGRAM NAME - OREGON/ODYSSEY HACK      VERSION:01/01/78-02/16/21
20 REM ORIGINAL PROGRAMMING BY BILL HEINEMANN - 1971
21 REM UPDATED VERSION BY RUBY OSTROW - 2021
30 REM SUPPORT RESEARCH AND MATERIALS BY DON RAVITSCH,
40 REM      MINNESOTA EDUCATIONAL COMPUTING CONSORTIUM STAFF
50 REM CDC CYBER 70/73-26     BASIC 3.1
60 REM DOCUMENTATION BOOKLET 'OREGON' AVAILABLE FROM
61 REM    MECC SUPPORT SERVICES
62 REM    2520 BROADWAY DRIVE
63 REM    ST. PAUL MN 55113
80 REM
150 REM  *FOR THE MEANING OF THE VARIABLES USED, LIST LINES 6510-6830*
155 REM
160 PRINT "DO YOU NEED INSTRUCTIONS (YES/NO)";
170 DIM C$(5)
180 REM RANDOMIZE REMOVED
190 INPUT C$
200 IF C$="NO" THEN 690
210 PRINT
220 PRINT
230 REM ***INSTRUCTIONS***
240 PRINT "THIS PROGRAM SIMULATES THE EPIC JOURNEY OF"
250 PRINT "THE GREEK HERO ODYSSEUS, RETURNING FROM THE TROJAN WAR"
260 PRINT "TO HIS HOME ON ROCKY ITHACA."
270 PRINT "YOU WILL PLAY AS THE CRAFTY ODYSSEUS"
280 PRINT “AND TRAVERSE THE WINE-DARK SEA WITH YOUR MEN”
290 PRINT “BUT BEWARE THE PERILS THAT LIE WITHIN.”
300 PRINT
310 PRINT "FROM YOUR SPOILS AT TROY, YOU HAVE 1000 DRACHMAS TO USE"
320 PRINT “TO EQUIP YOURSELF AND YOU HAVE SPENT 300”
330 PRINT “ON YOUR TRIREME ALREADY.”
340 PRINT "YOU WILL NEED TO SPEND THE REST OF YOUR MONEY ON THE"
350 PRINT "   FOLLOWING ITEMS:"
360 PRINT
370 PRINT "     SAILS AND OARS- YOU CAN SPEND $200-$300 ON THESE"
380 PRINT "            THE MORE YOU SPEND, THE FASTER YOU'LL GO"
390 PRINT "               BECAUSE YOU'LL HAVE A BETTER PREPARED SHIP"
400 PRINT
410 PRINT "     FOOD- THE MORE YOU HAVE, THE LESS CHANCE THERE"
420 PRINT "               IS OF GETTING SICK AND THE LESS CHANCE OF NOT"
421 PRINT “                  BEING ABLE TO SACRIFICE TO THE GODS”

430 PRINT
450 PRINT "     AMMUNITION - $1 BUYS A QUIVER OF 50 ARROWS"
460 PRINT "            YOU WILL NEED ARROWS FOR ATTACKS BY MONSTERS"
470 PRINT "               AND ENEMIES, AND FOR HUNTING FOOD"
480 PRINT
490 PRINT "     CLOTHING - THIS IS ESPECIALLY IMPORTANT FOR THE COLD"
500 PRINT "               WEATHER YOU WILL ENCOUNTER WHEN CROSSING"
510 PRINT "               THE ROUGH SEAS"
520 PRINT
530 PRINT "     MISCELLANEOUS SUPPLIES - THIS INCLUDES MEDICINE AND"
540 PRINT "               OTHER THINGS YOU WILL NEED FOR SICKNESS"
550 PRINT "               AND EMERGENCY REPAIRS"
560 PRINT
570 PRINT
580 PRINT "YOU CAN SPEND ALL YOUR MONEY BEFORE YOU START YOUR TRIP -"
590 PRINT "OR YOU CAN SAVE SOME OF YOUR CASH TO SPEND IN TOWNS ALONG"
600 PRINT "THE WAY WHEN YOU RUN LOW. HOWEVER, ITEMS COST MORE IN"
610 PRINT "GREEK MARKETS. YOU CAN ALSO GO HUNTING ALONG THE WAY TO GET"
620 PRINT "MORE FOOD."
630 PRINT "WHENEVER YOU MUST DRAW YOUR BOW AND ARROWS”
631 PRINT “ TO FEND OFF AN ENEMY,”
640 PRINT "YOU WILL BE TOLD TO TYPE IN A WORD (ONE THAT SOUNDS LIKE A "
650 PRINT "SHOT). THE FASTER YOU TYPE IN THAT WORD AND HIT THE ""RETURN"""
660 PRINT "KEY, THE BETTER LUCK YOU'LL HAVE WITH VANQUISHING YOUR ENEMY."
670 PRINT
680 PRINT "AT EACH TURN, ALL ITEMS ARE SHOWN IN DRACHMA AMOUNTS"
690 PRINT "EXCEPT ARROWS."
700 PRINT "WHEN ASKED TO ENTER DRACHMA AMOUNTS, DON'T USE A ""$""."
710 PRINT
720 PRINT "MAY THE GODS BE WITH YOU!!!"
730 PRINT
740 PRINT
750 PRINT "HOW GOOD A SHOT ARE YOU WITH YOUR BOW?"
760 PRINT "  (1) ACE MARKSMAN,  (2) GOOD SHOT,  (3) FAIR TO MIDDLIN'"
770 PRINT "         (4) NEED MORE PRACTICE,  (5) SHAKY KNEES"
780 PRINT "ENTER ONE OF THE ABOVE -- THE BETTER YOU CLAIM YOU ARE, THE"
790 PRINT "FASTER YOU'LL HAVE TO BE WITH YOUR BOW TO BE SUCCESSFUL."
800 INPUT D9
810 IF D9>5 THEN 830
820 GOTO 850
830 D9=0
840 REM ***INITIAL PURCHASES***
850 X1=-1
860 REM LINES 860-869 MODIFIED BY CHRISTOPHER PEDERSEN (AUG 10, 2018)
861 REM FOR COMPATIBILITY WITH THE CHIPMUNK BASIC INTERPRETER
862 REM CHIPMUNK BASIC: http://www.nicholson.com/rhn/basic/
863 D3=0
864 M9=0
865 M=0
866 F2=0
867 F1=0
868 S4=0
869 K8=0
870 PRINT
880 PRINT
890 PRINT "HOW MUCH DO YOU WANT TO SPEND ON YOUR SHIP MODIFICATIONS?";
900 INPUT A
910 IF A >= 200 THEN 940
920 PRINT "NOT ENOUGH"
930 GOTO 890
940 IF A <= 300 THEN 970
950 PRINT "TOO MUCH"
960 GOTO 890
970 PRINT "HOW MUCH DO YOU WANT TO SPEND ON FOOD";
980 INPUT F
990 IF F >= 0 THEN 1020
1000 PRINT "IMPOSSIBLE"
1010 GOTO 970
1020 PRINT "HOW MUCH DO YOU WANT TO SPEND ON AMMUNITION";
1030 INPUT B
1040 IF B >= 0 THEN 1070
1050 PRINT "IMPOSSIBLE"
1060 GOTO 1020
1070 PRINT "HOW MUCH DO YOU WANT TO SPEND ON CLOTHING";
1080 INPUT C
1090 IF C >= 0 THEN 1120
1100 PRINT "IMPOSSIBLE"
1110 GOTO 1070
1120 PRINT "HOW MUCH DO YOU WANT TO SPEND ON MISCELLANEOUS SUPPLIES";
1130 INPUT M1
1140 IF M1 >= 0 THEN 1170
1150 PRINT "IMPOSSIBLE"
1160 GOTO 1120
1170 T=700-A-F-B-C-M1
1180 IF T >= 0 THEN 1210
1190 PRINT "YOU OVERSPENT--YOU ONLY HAD $700 TO SPEND.  BUY AGAIN"
1200 GOTO 870
1210 B=50*B
1220 PRINT "AFTER ALL YOUR PURCHASES. YOU NOW HAVE ";T;" DRACHMAS LEFT"
1230 PRINT
1240 PRINT "MONDAY MAY 10 1200 BCE (ISH)"
1250 PRINT
1260 GOTO 1790
1270 IF M >= 2040 THEN 5470
1280 REM ***SETTING DATE***
1290 D3=D3+1
1300 PRINT
1310 PRINT "MONDAY ";
1320 IF D3>10 THEN 1340
1330 ON D3 GOTO 1350, 1370, 1390, 1410, 1430, 1450, 1470, 1490, 1510, 1530
1340 ON D3-10 GOTO 1550, 1570, 1590, 1610, 1630, 1650, 1670, 1690, 1710, 1730
1350 PRINT "SEPTEMBER 13 ";
1360 GOTO 1760
1370 PRINT "APRIL 5 ";
1380 GOTO 1760
1390 PRINT "OCTOBER 31 ";
1400 GOTO 1760
1410 PRINT “MAY 24 ";
1420 GOTO 1760
1430 PRINT "JUNE 7 ";
1440 GOTO 1760
1450 PRINT "JANUARY 21 ";
1460 GOTO 1760
1470 PRINT "JULY 5 ";
1480 GOTO 1760
1490 PRINT "MARCH 5 ";
1500 GOTO 1760
1510 PRINT "AUGUST 2 ";
1520 GOTO 1760
1530 PRINT “NOVEMBER 16 ";
1540 GOTO 1760
1550 PRINT "MAY 31 ";
1560 GOTO 1760
1570 PRINT “DECEMBER 13 ";
1580 GOTO 1760
1590 PRINT "SEPTEMBER 27 ";
1600 GOTO 1760
1610 PRINT “FEBRUARY 11 ";
1620 GOTO 1760
1630 PRINT "AUGUST 25  ";
1640 GOTO 1760
1650 PRINT "MAY 8 ";
1660 GOTO 1760
1670 PRINT "SEPTEMBER 22 ";
1680 GOTO 1760
1690 PRINT "DECEMBER 6 ";
1700 GOTO 1760
1710 PRINT "FEBRUARY 16 ";
1720 GOTO 1760
1730 PRINT "YOU HAVE BEEN ON THE SEAS TOO LONG  ------"
1740 PRINT "YOUR MEN DROWN IN THE HARSH WINTER WATERS"
1750 GOTO 5210
1760 PRINT "AROUND 1200 BCE"
1770 PRINT
1780 REM ***BEGINNING EACH TURN***
1790 IF F >= 0 THEN 1810
1800 F=0
1810 IF B >= 0 THEN 1830
1820 B=0
1830 IF C >= 0 THEN 1850
1840 C=0
1850 IF M1 >= 0 THEN 1870
1860 M1=0
1870 IF F >= 13 THEN 1890
1880 PRINT "YOU'D BETTER DO SOME HUNTING OR BUY FOOD AND SOON!!!!"
1890 F=INT(F)
1900 B=INT(B)
1910 C=INT(C)
1920 M1=INT(M1)
1930 T=INT(T)
1940 M=INT(M)
1950 M2=M
1960 IF S4=1 THEN 1990
1970 IF K8=1 THEN 1990
1980 GOTO 2030
1990 T=T-20
2000 IF T<0 THEN 5120
2010 PRINT "HEALER'S BILL IS 20 DRACHMAS"
2020 REM LINES 2020-2022 MODIFIED BY C.D.P. FOR COMPATIBILITY WITH MODERN BASIC
2021 LET S4=0
2022 LET K8=S4
2030 IF M9=1 THEN 2060
2040 PRINT "TOTAL MILEAGE: ";M
2050 GOTO 2080
2060 PRINT "TOTAL MILEAGE: 950"
2070 M9=0
2080 REM LINES 2080-2090 MODIFIED BY C.D.P. FOR COMPATIBILITY WITH MODERN BASIC
2081 PRINT "FOOD: ";F
2082 PRINT "ARROWS: ";B
2083 PRINT "CLOTHING: ";C
2084 PRINT "MISC. SUPPLIES: ";M1
2090 PRINT "DRACHMAS: $";T
2100 IF X1=-1 THEN 2210
2110 X1=X1*(-1)
2120 PRINT "DO YOU WANT TO (1) STOP AT THE NEXT TOWN, (2) HUNT, ";
2130 PRINT "OR (3) CONTINUE JOURNEYING"
2140 INPUT X
2150 REM IF X>2 THEN 2190
2160 REM IF X<1 THEN 2190
2170 REM LET X=INT(X)
2180 GOTO 2310
2190 LET X=3
2200 GOTO 2310
2210 PRINT "DO YOU WANT TO (1) HUNT, OR (2) CONTINUE"
2220 INPUT X
2230 IF X=1 THEN 2250
2240 LET X=2
2250 LET X=X+1
2260 IF X=3 THEN 2300
2270 IF B>39 THEN 2300
2280 PRINT "TOUGH---YOU NEED MORE ARROWS TO GO HUNTING"
2290 GOTO 2210
2300 X1=X1*(-1)
2310 ON X GOTO 2330,2580,2760
2320 REM ***STOPPING AT TOWN***
2330 PRINT "ENTER WHAT YOU WISH TO SPEND ON THE FOLLOWING"
2340 PRINT "FOOD";
2350 GOSUB 2370
2360 GOTO 2450
2370 INPUT P
2380 IF P<0 THEN 2440
2390 T=T-P
2400 IF T >= 0 THEN 2440
2410 PRINT "YOU DON'T HAVE THAT MUCH--KEEP YOUR SPENDING DOWN"
2420 T=T+P
2430 P=0
2440 RETURN
2450 F=F+2/3*P
2460 PRINT "ARROWS";
2470 GOSUB 2370
2480 LET B=INT(B+2/3*P*50)
2490 PRINT "CLOTHING";
2500 GOSUB 2370
2510 C=C+2/3*P
2520 PRINT "MISCELLANEOUS SUPPLIES";
2530 GOSUB 2370
2540 M1=M1+2/3*P
2550 M=M-45
2560 GOTO 2760
2570 REM ***HUNTING***
2580 IF B>39 THEN 2610
2590 PRINT "TOUGH---YOU NEED MORE ARROWS TO GO HUNTING"
2600 GOTO 2120
2610 M=M-45
2620 GOSUB 6180
2630 IF B1 <= 1 THEN 2700
2640 IF 100+RND(1)<13*B1 THEN 2750
2650 F=F+48-2*B1
2660 PRINT "NICE SHOT--RIGHT ON TARGET--WE WILL FEAST TONIGHT!!"
2670 B=B-10-3*B1
2680 GOTO 2760
2690 REM **BELLS IN LINE 2700**
2700 PRINT "THE WINDS HAVE CARRIED THE ARROW STRAIGHT--WHAT A FAIR BEAST!"
2710 PRINT "A FEAST WILL BE HAD!"
2720 F=F+52+RND(1)*6
2730 B=B-10-RND(1)*4
2740 GOTO 2760
2750 PRINT "ATHENA WAS NOT WITH YOU ON THAT ONE; DINNER GOT AWAY....."
2760 IF F >= 13 THEN 2790
2770 GOTO 5100
2780 REM ***EATING***
2790 PRINT "DO YOU WANT TO EAT (1) POORLY  (2) MODERATELY"
2800 PRINT "OR (3) LIKE THE GODS";
2810 INPUT E
2820 IF E>3 THEN 2790
2830 IF E<1 THEN 2790
2840 LET E=INT(E)
2850 LET F=F-8-5*E
2860 IF F >= 0 THEN 2900
2870 F=F+8+5*E
2880 PRINT "YOU ARE NOT THE GREAT ZEUS, YOU CAN'T EAT THAT WELL"
2890 GOTO 2790
2900 LET M=M+200+(A-220)/5+10*RND(1)
2910 REM LINES 2910-2912 MODIFIED BY C.D.P. FOR COMPATIBILITY W/ CHIPMUNK BASIC
2911 C1=0
2912 L1=C1
2920 REM ***SAILORS ATTACK***
2929 REM LINE 2730 MODIFIED BY C.D.P. FOR COMPATIBILITY WITH CHIPMUNK BASIC
2930 IF RND(1)*10>((M/100-4)^2+72)/((M/100-4)^2+12)-1 THEN 3590
2940 PRINT "SAILORS AHEAD.  THEY ";
2950 S5=0
2959 REM ALL RND(-1) FUNCTION CALLS HAVE BEEN CHANGED TO RND(1) BY C.D.P.
2960 IF RND(1)<.8 THEN 2990
2970 PRINT "DON'T ";
2980 S5=1
2990 PRINT "LOOK HOSTILE"
3000 PRINT "TACTICS"
3010 PRINT "(1) RUN  (2) ATTACK  (3) CONTINUE  (4) CIRCLE TRIREMES"
3020 IF RND(1)>.2 THEN 3040
3030 S5=1-S5
3040 INPUT T1
3050 IF T1<1 THEN 3010
3060 IF T1>4 THEN 3010
3070 T1=INT(T1)
3080 IF S5=1 THEN 3370
3090 IF T1>1 THEN 3150
3100 M=M+20
3110 M1=M1-15
3120 B=B-150
3130 A=A-40
3140 GOTO 3510
3150 IF T1>2 THEN 3280
3160 GOSUB 6180
3170 B=B-B1*40-80
3180 IF B1>1 THEN 3210
3190 PRINT "THE GODS WERE ON YOUR SIDE TODAY---YOU DROVE THEM OFF"
3200 GOTO 3510
3210 IF B1 <= 4 THEN 3260
3220 PRINT "AND I THOUGHT YOU WERE A WARRIOR---YOU GOT SPEARED "
3230 K8=1
3240 PRINT "YOU AND THE HEALER BETTER PRAY TO APOLLO QUICK"
3250 GOTO 3510
3260 PRINT "KINDA SLOW WITH YOUR COLT .45"
3270 GOTO 3510
3280 IF T1>3 THEN 3330
3290 IF RND(1)>.8 THEN 3490
3300 LET B=B-150
3310 M1=M1-15
3320 GOTO 3510
3330 GOSUB 6180
3340 B=B-B1*30-80
3350 M=M-25
3360 GOTO 3180
3370 IF T1>1 THEN 3410
3380 M=M+15
3390 A=A-10
3400 GOTO 3510
3410 IF T1>2 THEN 3450
3420 M=M-5
3430 B=B-100
3440 GOTO 3510
3450 IF T1>3 THEN 3470
3460 GOTO 3510
3470 M=M-20
3480 GOTO 3510
3490 PRINT "THEY DID NOT ATTACK"
3500 GOTO 3590
3510 IF S5=0 THEN 3540
3520 PRINT "SAILORS WERE FRIENDLY, BUT CHECK FOR POSSIBLE LOSSES"
3530 GOTO 3590
3540 PRINT "SAILORS WERE HOSTILE--CHECK FOR LOSSES"
3550 IF B >= 0 THEN 3590
3560 PRINT "YOU RAN OUT OF ARROWS AND GOT MASSACRED BY THE RIDERS"
3570 GOTO 5210
3580 REM ***SELECTION OF EVENTS***
3590 LET D1=0
3600 RESTORE
3610 R1=100*RND(1)
3620 LET D1=D1+1
3630 IF D1=16 THEN 4710
3640 READ D
3650 IF R1>D THEN 3620
3660 DATA 6,11,13,15,17,22,32,35,37,42,44,54,64,69,95
3670 IF D1>10 THEN 3690
3680 ON D1 GOTO 3700,3740,3780,3830,3860,3890,3920,4000,4170,4230
3690 ON D1-10 GOTO 4260,4330,4380,4600,4650,4710
3700 PRINT "MUST PASS BY SCYLLA AND CHARYBDIS--LOSE TIME AND SUPPLIES"
3710 LET M=M-15-5*RND(1)
3720 LET M1=M1-8
3730 GOTO 4750
3740 PRINT "MEN EATEN BY THE CYCLOPS---SLOWS YOU DOWN REST OF TRIP"
3750 LET M=M-25
3760 LET A=A-20
3770 GOTO 4750
3780 PRINT "BACK LUCK---YOUR MEN WERE TURNED INTO PIGS BY CIRCE"
3790 PRINT "YOU HAD TO STOP AND APPEAL TO THE GODDESS FOR THEIR RETURN"
3800 M=M-5-4*RND(1)
3810 M1=M1-2-3*RND(1)
3820 GOTO 4750
3830 PRINT "YOU WANT TO HEAR THE SIRENS---SLOW GOING PAST THEIR ROCKS"
3840 M=M-17
3850 GOTO 4750
3860 PRINT "YOU MUST SPEAK TO PROPHET TIRESIAS---DETOUR TO HADES"
3870 M=M-10
3880 GOTO 4750
3890 PRINT "LURED IN BY THE LOTUS EATERS--LOSE TIME TRYING TO ESCAPE"
3900 LET M=M-10*RND(1)-2
3910 GOTO 4750
3920 IF M>950 THEN 4530
3930 PRINT "YOU HAVE ANGERED POSEIDON---TIME AND SUPPLIES LOST"
3950 F=F-10
3960 B=B-500
3970 M1=M1-15
3980 M=M-10*RND(1)-5
3990 GOTO 4750
4000 PRINT "LAESTRYGONIANS ATTACK"
4010 GOSUB 6180
4020 B=B-20*B1
4030 IF B >= 0 THEN 4070
4040 PRINT "YOU RAN OUT OF ARROWS---SHIPS WITH MEN AND WAR SPOILS SUNK"
4050 T=T/3
4060 GOTO 4080
4070 IF B1 <= 1 THEN 4140
4080 PRINT "YOU GOT SHOT IN THE LEG AND THEY DAMAGED YOUR TRIREME"
4090 K8=1
4100 PRINT "BETTER HAVE A HEALER LOOK AT YOUR WOUND"
4110 M1=M1-5
4120 A=A-20
4130 GOTO 4750
4140 PRINT "BLESSED BY THE GODS TODAY!!!"
4150 PRINT "YOU GOT 'EM!"
4160 GOTO 4750
4170 PRINT "THERE WAS A STORM IN THE NIGHT--FOOD AND SUPPLIES DAMAGE!"
4180 F=F-40
4190 B=B-400
4200 LET M1=M1-RND(1)*8-3
4210 M=M-15
4220 GOTO 4750
4230 PRINT "WINDS BLOW YOU OFF COURSE---TIME IS LOST"
4240 M=M-10-5*RND(1)
4250 GOTO 4750
4260 PRINT "YOU BLINDED POLYPHEMUS AFTER HE INJURED YOU"
4270 B=B-10
4280 M1=M1-5
4290 IF M1 >= 0 THEN 4320
4300 PRINT "YOU DIE SINCE YOU HAVE NO MEDICINE"
4310 GOTO 5210
4320 GOTO 4750
4330 PRINT "NEAR SHIPWRECK!--LOSE FOOD AND CLOTHES"
4340 F=F-30
4350 C=C-20
4360 M=M-20-20*RND(1)
4370 GOTO 4750
4380 PRINT "SCYLLA ATTACKS!"
4390 GOSUB 6180
4400 IF B>39 THEN 4450
4410 PRINT "YOU WERE TOO LOW ON ARROWS--"
4420 PRINT "SHE OVERPOWERED YOU"
4430 K8=1
4440 GOTO 5160
4450 IF B1>2 THEN 4480
4460 PRINT "NICE SHOOTING AND ROWING---SHE DIDN’T EAT MANY OF YOU"
4470 GOTO 4490
4480 PRINT "TOO SLOW!---SHE GOT AT YOUR MEN, FOOD, AND CLOTHES"
4490 B=B-20*B1
4500 C=C-B1*4
4510 F=F-B1*8
4520 GOTO 4750
4530 PRINT "BRRRRRRR! AEOLUS’ WINDS RUN FREE TONIGHT---YOU ";
4540 IF C>22+4*RND(1) THEN 4570
4550 PRINT "DON'T ";
4560 C1=1
4570 PRINT "HAVE ENOUGH CLOTHING TO KEEP YOU WARM"
4580 IF C1=0 THEN 4750
4590 GOTO 6340
4600 PRINT "WAVES CRASH OVER THE SHIP---SUPPLIES DAMAGED"
4610 M=M-5-RND(1)*10
4620 B=B-200
4630 M1=M1-4-RND(1)*3
4640 GOTO 4750
4650 IF E=1 THEN 6340
4660 IF E=3 THEN 4690
4670 IF RND(1)>.25 THEN 6340
4680 GOTO 4750
4690 IF RND(1)<.5 THEN 6340
4700 GOTO 4750
4710 PRINT "ATHENA COMES TO YOUR AID AND GUIDES YOU TO MORE FOOD"
4720 F=F+14
4730 GOTO 4750
4740 REM ***MOUNTAINS***
4750 IF M <= 950 THEN 1230
4759 REM LINE 4720 MODIFIED BY C.D.P. FOR COMPATIBILITY WITH CHIPMUNK BASIC
4760 IF RND(1)*10>9-((M/100-15)^2+72)/((M/100-15)^2+12) THEN 4860
4770 PRINT "NARROW CLIFF PASS"
4780 IF RND(1)>.1 THEN 4820
4790 PRINT "SHIP STUCK ON THE ROCKS---LOSE VALUABLE TIME DRAGGING IT OFF!"
4800 M=M-60
4810 GOTO 4900
4820 IF RND(1)>.11 THEN 4880
4830 PRINT "YOU HIT THE CLIFF!---LOSE TIME AND SUPPLIES"
4840 M1=M1-5
4850 B=B-200
4860 M=M-20-30*RND(1)
4870 GOTO 4900
4880 PRINT "THE GOING GETS SLOW"
4890 M=M-45-RND(1)/.02
4900 IF F1=1 THEN 4940
4910 F1=1
4920 IF RND(1)<.8 THEN 5010
4930 PRINT "YOU MADE IT SAFELY THROUGH THE PASS--NO STORM"
4940 IF M<1700 THEN 4980
4950 IF F2=1 THEN 4980
4960 F2=1
4970 IF RND(1)<.7 THEN 5010
4980 IF M>950 THEN 1270
4990 M9=1
5000 GOTO 1270
5010 PRINT "STORM STRUCK WHILE IN CLIFF PASS--TIME AND SUPPLIES LOST"
5020 L1=1
5030 F=F-25
5040 M1=M1-10
5050 B=B-300
5060 M=M-30-40*RND(1)
5070 IF C<18+2*RND(1) THEN 6340
5080 GOTO 4980
5090 REM ***DYING***
5100 PRINT "YOU RAN OUT OF FOOD AND STARVED TO DEATH"
5110 GOTO 5210
5120 LET T=0
5130 PRINT "YOU CAN'T AFFORD A HEALER"
5140 GOTO 5160
5150 PRINT "YOU RAN OUT OF MEDICINES"
5160 PRINT "YOU DIED OF ";
5170 IF K8=1 THEN 5200
5180 PRINT "DROWNING"
5190 GOTO 5210
5200 PRINT "INJURIES"
5210 PRINT
5220 PRINT "DUE TO YOUR UNFORTUNATE SITUATION, THERE ARE A FEW"
5230 PRINT "FORMALITIES WE MUST GO THROUGH"
5240 PRINT
5250 PRINT "WOULD YOU LIKE A PRIEST?"
5260 INPUT C$
5270 PRINT "WOULD YOU LIKE A HERO’S PYRE?"
5280 INPUT C$
5290 PRINT "WOULD YOU LIKE US TO SEND WORD TO PENELOPE AND TELEMACHUS?"
5300 INPUT C$
5310 IF C$="YES" THEN 5350
5320 PRINT "BUT YOUR WIFE AND SON HAVE WAITED SO LONG FOR YOU ON ITHACA"
5330 PRINT
5340 GOTO 5370
5350 PRINT "THAT WILL BE 4.50 DRACHMAS FOR THE MESSENGER BIRD."
5360 PRINT
5370 PRINT "WE THANK YOU FOR THIS INFORMATION, ODYSSEUS, AND WE "
5380 PRINT "ARE SORRY YOU DIDN'T MAKE IT HOME TO ITHACA"
5390 PRINT "BETTER LUCK NEXT TIME. MAY YOU HAVE A BLESSED AFTERLIFE."
5400 PRINT
5410 PRINT
5420 PRINT TAB(30);"SINCERELY"
5430 PRINT
5440 PRINT TAB(17);"THE OLYMPIANS <3"
5449 REM 'STOP' COMMAND BELOW CHANGED TO 'END' BY C.D.P.
5450 END
5460 REM ***FINAL TURN***
5470 F9=(2040-M2)/(M-M2)
5480 F=F+(1-F9)*(8+5*E)
5490 PRINT
5500 REM **BELLS IN LINES 5510,5520**
5510 PRINT "YOU FINALLY ARRIVED IN ITHACA"
5520 PRINT "AFTER MANY LONG MILES---HOORAY!!!!!"
5530 PRINT "A REAL HERO! NOW GOOD LUCK IN RECLAIMING YOUR THRONE!"
5540 PRINT
5550 F9=INT(F9*14)
5560 D3=D3*14+F9
5570 F9=F9+1
5580 IF F9<8 THEN 5600
5590 F9=F9-7
5600 ON F9 GOTO 5610,5630,5650,5670,5690,5710,5730
5610 PRINT "MONDAY ";
5620 GOTO 5740
5630 PRINT "TUESDAY ";
5640 GOTO 5740
5650 PRINT "WEDNESDAY ";
5660 GOTO 5740
5670 PRINT "THURSDAY ";
5680 GOTO 5740
5690 PRINT "FRIDAY ";
5700 GOTO 5740
5710 PRINT "SATURDAY ";
5720 GOTO 5740
5730 PRINT "SUNDAY ";
5740 IF D3>124 THEN 5780
5750 D3=D3-93
5760 PRINT "JULY ";D3;" 1200 BCE"
5770 GOTO 5960
5780 IF D3>155 THEN 5820
5790 D3=D3-124
5800 PRINT "AUGUST ";D3;" 1198 BCE"
5810 GOTO 5960
5820 IF D3>185 THEN 5860
5830 D3=D3-155
5840 PRINT "SEPTEMBER ";D3;" 1196 BCE"
5850 GOTO 5960
5860 IF D3>216 THEN 5900
5870 D3=D3-185
5880 PRINT "OCTOBER ";D3;" 1194 BCE"
5890 GOTO 5960
5900 IF D3>246 THEN 5940
5910 D3=D3-216
5920 PRINT "NOVEMBER ";D3;" 1192 BCE"
5930 GOTO 5960
5940 D3=D3-246
5950 PRINT "DECEMBER ";D3;" 1190 BCE"
5960 PRINT
5970 REM LINE 5930 REMOVED BY C.D.P. FOR COMPATIBILITY WITH MODERN BASIC
5980 IF B>0 THEN 6000
5990 LET B=0
6000 IF C>0 THEN 6020
6010 LET C=0
6020 IF M1>0 THEN 6040
6030 LET M1=0
6040 IF T>0 THEN 6060
6050 LET T=0
6060 IF F>0 THEN 6080
6070 LET F=0
6080 REM LINES 6080-6085 MODIFIED BY C.D.P. FOR COMPATIBILITY WITH MODERN BASIC
6081 PRINT "FOOD: ";F
6082 PRINT "ARROWS: ";B
6083 PRINT "CLOTHING: ";C
6084 PRINT "MISC. SUPP.: ";M1
6085 PRINT "DRACHMAS: ";T
6090 PRINT
6100 PRINT TAB(11); "ZEUS SENDS YOU HIS"
6110 PRINT TAB(17); "HEARTIEST CONGRATULATIONS"
6120 PRINT
6130 PRINT TAB(11);"AND WISHES YOU A PROSPEROUS LIFE AHEAD"
6140 PRINT
6150 PRINT TAB(22);"AT HOME"
6159 REM 'STOP' COMMAND BELOW CHANGED TO 'END' BY C.D.P.
6160 END
6170 REM ***SHOOTING SUB-ROUTINE***
6171 REM THE METHOD OF TIMING THE SHOOTING (LINES 6210-6240)
6172 REM WILL VARY FROM SYSTEM TO SYSTEM. FOR EXAMPLE, H-P
6173 REM USERS WILL PROBABLY PREFER TO USE THE 'ENTER' STATEMENT.
6174 REM IF TIMING ON THE USER'S SYSTEM IS HIGHLY SUSCEPTIBLE
6175 REM TO SYSTEM RESPONSE TIME, THE FORMULA IN LINE 6240 CAN
6176 REM BE TAILORED TO ACCOMMODATE THIS BY EITHER INCREASING
6177 REM OR DECREASING THE 'SHOOTING' TIME RECORDED BY THE SYSTEM
6180 DIM S$(5)
6190 S$(1)="TWANG"
6200 S$(2)="WHOOSH"
6210 S$(3)="WHAM"
6220 S$(4)="THWAP"
6230 S6=INT(RND(1)*4+1)
6240 PRINT "TYPE "; S$(S6)
6246 REM 'CLK(0)' FUNCTION CHANGED TO 'TIMER' BY C.D.P. FOR COMPATIBILITY
6247 REM B3 = TIMER changed to B3 = TIMER + 2 BY C.D.P. TO MAKE HUNTING...
6248 REM ...EASIER ON MODERN COMPUTERS. SEE NOTES FROM ORIGINAL AUTHOR ON
6249 REM ... ON TWEAKING HUNTING DIFFICULTY
6250 B3 = TIMER + 2
6260 INPUT C$
6269 REM 'CLK(0)' FUNCTION CHANGED TO 'TIMER' BY C.D.P. FOR COMPATIBILITY
6270 B1 = TIMER
6280 B1=((B1-B3)*3600)-(D9-1)
6290 PRINT
6295 IF B1>0 THEN 6300
6297 B1=0
6300 IF C$=S$(S6) THEN 6320
6310 B1=9
6320 RETURN
6330 REM ***ILLNESS SUB-ROUTINE***
6340 IF 100*RND(1)<10+35*(E-1) THEN 6410
6349 REM LINE 6350 MODIFIED BY C.D.P. FOR COMPATIBILITY WITH CHIPMUNK BASIC
6350 IF 100*RND(1)<100-(40/4^(E-1)) THEN 6450
6360 PRINT "SERIOUS ILLNESS---"
6370 PRINT "YOU MUST STOP FOR MEDICAL ATTENTION"
6380 M1=M1-10
6390 S4=1
6400 GOTO 6480
6410 PRINT "WILD ILLNESS---MEDICINE USED"
6420 M=M-5
6430 M1=M1-2
6440 GOTO 6480
6450 PRINT "BAD ILLNESS---MEDICINE USED"
6460 M=M-5
6470 M1=M1-5
6480 IF M1<0 THEN 5150
6490 IF L1=1 THEN 4980
6500 GOTO 4750
6510 REM ***IDENTIFICATION OF VARIABLES IN THE PROGRAM***
6520 REM A = AMOUNT SPENT ON SHIP
6530 REM B = AMOUNT SPENT ON ARROWS
6540 REM B1 = ACTUAL RESPONSE TIME FOR INPUTTING "TWANG"
6550 REM B3 = CLOCK TIME AT START OF INPUTTING "TWANG"
6560 REM C = AMOUNT SPENT ON CLOTHING
6570 REM C1 = FLAG FOR INSUFFICIENT CLOTHING IN BAD WEATHER
6580 REM C$ = YES/N0 RESPONSE TO QUESTIONS
6590 REM D1 = COUNTER IN GENERATING EVENTS
6600 REM D3 = TURN NUMBER FOR SETTING DATE
6610 REM D4 = CURRENT DATE
6620 REM D9 = CHOICE OF SHOOTING EXPERTISE LEVEL
6630 REM E = CHOICE OF EATING
6640 REM F = AMOUNT SPENT ON FOOD
6650 REM F1 = FLAG FOR CLEARING CLIFF PASS
6660 REM F2 = FLAG FOR CLEARING OCEAN
6670 REM F9 = FRACTION OF 2 WEEKS TRAVELED ON FINAL TURN
6680 REM K8 = FLAG FOR INJURY
6690 REM L1 = FLAG FOR STORM
6700 REM M = TOTAL MILEAGE WHOLE TRIP
6710 REM M1 = AMOUNT SPENT ON MISCELLANEOUS SUPPLIES
6720 REM M2 = TOTAL MILEAGE UP THROUGH PREVIOUS TURN
6730 REM M9 = FLAG FOR CLEARING CLIFF PASS IN SETTING MILEAGE
6740 REM P = AMOUNT SPENT ON ITEMS AT TOWN
6750 REM R1 = RANDOM NUMBER IN CHOOSING EVENTS
6760 REM S4 = FLAG FOR ILLNESS
6770 REM S5 = ""HOSTILITY OF ENEMIES"" FACTOR
6780 REM S6 = SHOOTING WORD SELECTOR
6790 REM S$ = VARIATIONS OF SHOOTING WORD
6800 REM T = CASH LEFT OVER AFTER INITIAL PURCHASES
6810 REM T1 = CHOICE OF TACTICS WHEN ATTACKED
6820 REM X = CHOICE OF ACTION FOR EACH TURN
6830 REM X1 = FLAG FOR TOWN OPTION
6840 END
